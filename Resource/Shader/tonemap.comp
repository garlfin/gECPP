#ifndef PIPELINE_COLOR_FORMAT
    #define PIPELINE_COLOR_FORMAT rgba16f
#endif

#ifndef GAMMA
    #define GAMMA 2.2
#endif

// IO
layout(binding = 0, PIPELINE_COLOR_FORMAT) restrict readonly uniform image2D ColorIn;
layout(binding = 1, PIPELINE_COLOR_FORMAT) restrict writeonly uniform image2D ColorOut;

// Helper Functions
vec3 ACESNarkowicz(vec3);

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
void main()
{
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);

    vec4 color = imageLoad(ColorIn, texel);

    color.rgb = ACESNarkowicz(color.rgb * 0.6);
    color.rgb = pow(color.rgb, vec3(1.0 / GAMMA));

    imageStore(ColorOut, texel, color);
}

// https://knarkowicz.wordpress.com/2016/01/06/aces-filmic-tone-mapping-curve/
vec3 ACESNarkowicz(vec3 x)
{
    float a = 2.51;
    float b = 0.03;
    float c = 2.43;
    float d = 0.59;
    float e = 0.14;

    return clamp((x * (a * x + b)) / (x * (c * x + d) + e), vec3(0.0), vec3(1.0));
}

